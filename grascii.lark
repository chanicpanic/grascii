
// todo
// move terms to rules to allow for ambigation resolution
// with overlapping terminals
//
// refine aspirates: before after root, before consonants
//
// add NG NK, X, Z ?
// Hook vowel orientation
// W placement
// intersection?
// priorities?
// S, TH, SH alt. pronunciations





start : _preroot? _root _postroot?
_preroot : prefix DISJOINER
_postroot : DISJOINER suffix
DISJOINER : "^"

_root : (_letter | ASPIRATE | _root "-"? _letter) ASPIRATE?

prefix : _root 
suffix : _root

_letter : consonant | ( ASPIRATE? vowel )

consonant : standard_consonant
	| BLENDED_CONSONANT
	| DIRECTED_CONSONANT

standard_consonant : STANDARD_CONSONANT | NGK

NGK : "NG" | "NK"

STANDARD_CONSONANT : "K" | "G" | "R" | "L" | "N" | "M"
	| "T" | "D" | "P" | "B" | "F" | "V" | "CH" 
	| "J" | "SH"

BLENDED_CONSONANT : "TN" | "DN" | "TM" | "DM"
	| "NT" | "MT" | "ND" | "MD"
	| "DF" | "DV" | "TV" 
	| "JNT" | "JND" | "PND" | "PNT"
	| "SS" | "XS"
	| "MN" | "MM"
	| "DT" | "TD" | "DD"

DIRECTED_CONSONANT : DIRECTED_CONSONANT_TERM DIRECTION?

DIRECTED_CONSONANT_TERM : "S" | "TH"
DIRECTION : ")" | "("

vowel : ( circle_vowel | hook_vowel | diphthong ) UNDERBAR? 

ASPIRATE : "'"
UNDERBAR : "_"

circle_vowel : CIRCLE_VOWEL CIRCLE_VOWEL_MODIFIER*

CIRCLE_VOWEL : "A" | "E"
CIRCLE_VOWEL_MODIFIER : "." | "," | "~" | "|"

hook_vowel : HOOK_VOWEL HOOK_VOWEL_MODIFIER*
HOOK_VOWEL : "O" | "U"
HOOK_VOWEL_MODIFIER : "." | ","

diphthong : DIPHTHONG | (I I_MODIFIERS*)
I : "I"
I_MODIFIERS : "~" | "|"
DIPHTHONG : "EU" | "AU" | "OE" | "A&'" | "A&E"













