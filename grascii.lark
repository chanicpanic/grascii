
// todo
// move terms to rules to allow for ambigation resolution
// with overlapping terminals
//
// refine aspirates: before after root, before consonants
//
// add NG NK, X, Z ?
// Hook vowel orientation
// W placement
// intersection?
// priorities?
// S, TH, SH alt. pronunciations
// add LD





start : _preroot? _root _postroot?
_preroot : prefix DISJOINER
_postroot : DISJOINER suffix
DISJOINER : "^"

_root : (_letter | ASPIRATE | _root "-"? _letter) ASPIRATE?

prefix : _root 
suffix : _root

_letter : consonant | ( ASPIRATE? vowel )

consonant : standard_consonant
	| blended_consonant
	| distinguishable_consonant

standard_consonant : STANDARD_CONSONANT | NGK

NGK : "NG" | "NK"

STANDARD_CONSONANT : "K" | "G" | "R" | "L" | "N" | "M"
	| "T" | "D" | "P" | "B" | "F" | "V" | "CH" | "J"

blended_consonant : BLENDED_CONSONANT

BLENDED_CONSONANT : "TN" | "DN" | "TM" | "DM"
	| "NT" | "MT" | "ND" | "MD"
	| "DF" | "DV" | "TV" 
	| "JNT" | "JND" | "PND" | "PNT"
	| "SS" | "XS"
	| "MN" | "MM"
	| "DT" | "TD" | "DD"

distinguishable_consonant : ( directed_consonant | SH ) OBLIQUE?

SH : "SH"
OBLIQUE : ","

directed_consonant : DIRECTED_CONSONANT DIRECTION?

DIRECTED_CONSONANT : "S" | "TH"
DIRECTION : ")" | "("

vowel : circle_vowel | hook_vowel | diphthong 

ASPIRATE : "'"
UNDERBAR : "_"

circle_vowel : CIRCLE_VOWEL CIRCLE_VOWEL_MODIFIER*

CIRCLE_VOWEL : "A" | "E"
CIRCLE_VOWEL_MODIFIER : "." | "," | "~" | "|" | UNDERBAR

hook_vowel : HOOK_VOWEL HOOK_VOWEL_MODIFIER*
HOOK_VOWEL : "O" | "U"
HOOK_VOWEL_MODIFIER : "." | "," | UNDERBAR

diphthong : ( DIPHTHONG UNDERBAR? ) | ( I I_MODIFIERS* )
I : "I"
I_MODIFIERS : "~" | "|" | UNDERBAR
DIPHTHONG : "EU" | "AU" | "OE" | "A&'" | "A&E"













